import components
import entity
import conditions
import connections
import repetition

// Each screen has a name, title, URL, optional description, and nested structure
Screen:
    'Screen' name=ID
    'title:' title=STRING
    'url:' url=STRING
    ('description:' description=STRING)?
    (
        (localEntities*=Entity)?    // Screen-specific entities
        (elements*=StructuralElement)
    )#
    'end'
;

// Rows and columns, which can contain more structural elements
StructuralElement:
    Row | Column | Link | Component | ComponentRef | Condition | Repetition
;

Row:
    'row' ('height:' height=INT)? elements*=StructuralElement 'endrow'
;

Column:
    'col' ('width:' width=INT)? elements*=StructuralElement 'endcol'
;

Link:
    'link' 
    'to:' url=STRING
    ('text:' text=STRING)?
;