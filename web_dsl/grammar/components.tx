import entity
import utils
import communication

Component:
    'Component' name=ID
    'type:' type=ComponentType
    'entity:'  entity=[Entity] 
    'end'
;

// 'schema:' attributes+=[Attribute|ID] (',' attributes+=[Attribute|ID])*

ComponentType:
    Button | Form | Image | h1 | h2 | p | Notification | LineChart | Logs | Gauge | JsonViewer | Alive | Publish
;

ReusableComponentDefinition:
    'DefineComponent' name=ID 
    'as' definition = Component
    'end'
;

ReusableComponent:
    'useComponent' ref=[ReusableComponentDefinition|ID]
;

Button:
    'Button' (name=ID)? content=STRING
;

Input:
    'Input' (name=ID)?
    (
        ('type:' type=InputType)
        ('placeholder:' placeholder=STRING)
        ('required:' required=BOOL)
    )#
;

InputType:
    'text' | 'password' | 'number' | 'email' | 'checkbox' | 'radio'
;

Label:
    'Label' (name=ID)? 'content:' content=STRING
;

Form:
    'Form' (name=ID)?
    elements*=FormElement
    'endform'
;

FormElement:
    Input | Label | Button
;


Image:
    'Image' (name=ID)?
    ( 
        ('width:' width=INT)?
        ('height:' height=INT)?
        ('source:' content=STRING)?
    )#
;

h1:
    'h1' (name=ID)? content=STRING
;

h2:
    'h2' (name=ID)? content=STRING
;

p:
    'p' (name=ID)? content=STRING
;

Notification:
    'Notification'
    ('type:' type=NotificationType)?
;

NotificationType:
    'info' | 'warning' | 'error' | 'success'
;

LineChart:
    'LineChart' (name=ID)? 
    'xLabel:' xLabel=STRING
    'yLabel:' yLabel=STRING
;

Logs:
    'Logs' (name=ID)?
;

Gauge:
    'Gauge' 
    'value:' value=[Attribute|ID]
;

JsonViewer:
    'JsonViewer' (name=ID)?
;

Alive:
    'Alive' (name=ID)?
    'timeout:' timeout=INT
;

Publish:
    'Publish' (name=ID)?
    'broker:' ref=[MessageBroker]
;