import entity
import utils
import communication

Component:
    'Component' name=ID
    (
        ('entity:'  entity=[Entity])?
        ('type:' type=ComponentType)
    )#
    'end'
;

ComponentRef:
    'use' ref=[Component]
;

ComponentType:
    Button | Form | Image | Text | Notification | LineChart | LiveTable | Gauge | JsonViewer | Alive | Publish | Logs
;

Button:
    'Button' (name=ID)? content=STRING
;

Input:
    'Input' (name=ID)?
    (
        ('type:' type=InputType)
        ('placeholder:' placeholder=STRING)
        ('required:' required=BOOL)
    )#
;

InputType:
    'text' | 'password' | 'number' | 'email' | 'checkbox' | 'radio'
;

Label:
    'Label' (name=ID)? 'content:' content=STRING
;

Form:
    'Form' (name=ID)?
    elements*=FormElement
    'endform'
;

FormElement:
    Input | Label | Button
;


Image:
    'Image'
    ( 
        ('width:' width=INT)?
        ('height:' height=INT)?
        ('source:' source=[Attribute|ID])?
    )#
;

Text:
    'Text' content=STRING
;

Notification:
    'Notification'
    ('type:' type=NotificationType)?
    'message:' message=[Attribute|ID]
;

NotificationType:
    'info' | 'warning' | 'error' | 'success'
;

LineChart:
    'LineChart' 
    (
        ('xLabel:' xLabel=STRING)
        ('yLabel:' yLabel=STRING)
        ('xValue:' xValue=[Attribute|ID])
        ('yValues:' yValues+=[Attribute|ID][','])
    )#
;

LiveTable:
    'LiveTable'
    'columns:' columns+=[Attribute|ID][',']
;

Gauge:
    'Gauge' 
    'value:' value=[Attribute|ID]
;

JsonViewer:
    'JsonViewer'
    ('attributes:' attributes+=[Attribute|ID][','])?
;

Alive:
    'Alive'
    'timeout:' timeout=INT
;

Publish:
    'Publish'
    'broker:' broker=[MessageBroker]
    (
        ('topic:' topic=STRING)?
        ('json:' json=STRING)?
    )#
;

Logs:
    'Logs'
    ('attributes:' attributes+=[Attribute|ID][','])
;