import entity
import utils
import communication

Component:
    'Component' name=ID
    (
        ('entity:'  entity=[Entity])?
        ('type:' type=ComponentType)
    )#
    'end'
;

ComponentRef:
    'use' ref=[Component]
;

ComponentType:
    Button | Form | Image | h1 | h2 | p | Notification | LineChart | Logs | Gauge | JsonViewer | Alive | Publish
;

Button:
    'Button' (name=ID)? content=STRING
;

Input:
    'Input' (name=ID)?
    (
        ('type:' type=InputType)
        ('placeholder:' placeholder=STRING)
        ('required:' required=BOOL)
    )#
;

InputType:
    'text' | 'password' | 'number' | 'email' | 'checkbox' | 'radio'
;

Label:
    'Label' (name=ID)? 'content:' content=STRING
;

Form:
    'Form' (name=ID)?
    elements*=FormElement
    'endform'
;

FormElement:
    Input | Label | Button
;


Image:
    'Image'
    ( 
        ('width:' width=INT)?
        ('height:' height=INT)?
        ('source:' source=[Attribute|ID])?
    )#
;

h1:
    'h1' content=STRING
;

h2:
    'h2' content=STRING
;

p:
    'p' content=STRING
;

Notification:
    'Notification'
    ('type:' type=NotificationType)?
    'message:' message=[Attribute|ID]
;

NotificationType:
    'info' | 'warning' | 'error' | 'success'
;

LineChart:
    'LineChart' 
    (
        ('xLabel:' xLabel=STRING)
        ('yLabel:' yLabel=STRING)
        ('xValue:' xValue=[Attribute|ID])
        ('yValues:' yValues+=[Attribute|ID][','])
    )#
;

Logs:
    'Logs' (name=ID)?
;

Gauge:
    'Gauge' 
    'value:' value=[Attribute|ID]
;

JsonViewer:
    'JsonViewer' (name=ID)?
;

Alive:
    'Alive' (name=ID)?
    'timeout:' timeout=INT
;

Publish:
    'Publish' (name=ID)?
    'broker:' ref=[MessageBroker]
;