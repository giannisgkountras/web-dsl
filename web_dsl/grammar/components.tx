import entity
import communication
import types
import accessors
import conditions


Component:
    'Component' name=ID
    (
        ('entity:'  entity=[Entity])?
        ('type:' type=ComponentType)
        (condition=Condition)?
    )#
    'end'
;

ComponentRef:
    'use' ref=[Component]
;

ComponentType:
    Button | Form | Image | Text | Notification | LineChart | LiveTable | Gauge | JsonViewer | Alive | Publish | Logs | CrudTable
;

Button:
    'Button' content=STRING
;

Input:
    'Input'
    (
        ('type:' type=InputType)
        ('placeholder:' placeholder=STRING)
        ('required:' required=BOOL)
    )#
;

InputType:
    'text' | 'password' | 'number' | 'email' | 'checkbox' | 'radio'
;

Label:
    'Label' 
        'content:' content=STRING
;

Form:
    'Form' (name=ID)?
    elements*=FormElement
    'endform'
;

FormElement:
    Input | Label | Button
;


Image:
    'Image'
    ( 
        ('width:' width=INT)?
        ('height:' height=INT)?
        ('source:' (source=NestedAccessPath | source_static=STRING))?
    )#
;

Text:
    'Text' 
    'content:' (
        content=NestedAccessPath                   
        | content_static=STRING               
    )
    (
        ('size:' size=INT)?
        ('color:' color=STRING)?
    )#
;

Notification:
    'Notification'
    (
        ('type:' type=NotificationType)?
        'message:' message=NestedAccessPath
    )#
;

NotificationType:
    'info' | 'warning' | 'error' | 'success'
;

LineChart:
    'LineChart' 
    (
        ('xLabel:' xLabel=STRING)
        ('yLabel:' yLabel=STRING)
        ('xValue:' (xValue=NestedAccessPath | xValue_static=STRING))
        ('yValues:' (yValues+=NestedAccessPath | yValues_static+=STRING[',']))
        ('staticData:' staticData=List)?
    )#
;

LiveTable:
    'LiveTable'
    'columns:' columns+=NestedAccessPath[',']
;

Gauge:
    'Gauge' 
    'value:' (value=NestedAccessPath |  value_static=FLOAT)
;

JsonViewer:
    'JsonViewer'
    ('attributes:' attributes+=NestedAccessPath[','])?
;

Alive:
    'Alive'
    'timeout:' timeout=INT
;

Publish:
    'Publish'
    ('broker:' broker=[MessageBroker])?
    ('endpoint:' endpoint=[RESTEndpoint])?
    (
        ('topic:' topic=STRING)?
        ('json:' json=STRING)?
    )#
;

Logs:
    'Logs'
    ('attributes:' attributes+=NestedAccessPath[','])
;

CrudTable:
    'CrudTable'
    ('attributes:' attributes+=NestedAccessPath[','])?
;

