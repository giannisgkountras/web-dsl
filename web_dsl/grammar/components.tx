import entity
import communication
import types

AttributeAccessor:
    '.' attribute=ID  
;

IndexAccessor:
    '[' index=INT ']'
;

Accessor:
    // One step in the access path can be either by attribute or by index
    AttributeAccessor | IndexAccessor
;

NestedAccessPath:
    base='this' accessors*=Accessor // Starts with 'this', followed by zero or more accessors
;

Component:
    'Component' name=ID
    (
        ('entity:'  entity=[Entity])?
        ('type:' type=ComponentType)
    )#
    'end'
;

ComponentRef:
    'use' ref=[Component]
;

ComponentType:
    Button | Form | Image | Text | Notification | LineChart | LiveTable | Gauge | JsonViewer | Alive | Publish | Logs | CrudTable
;

Button:
    'Button' content=STRING
;

Input:
    'Input'
    (
        ('type:' type=InputType)
        ('placeholder:' placeholder=STRING)
        ('required:' required=BOOL)
    )#
;

InputType:
    'text' | 'password' | 'number' | 'email' | 'checkbox' | 'radio'
;

Label:
    'Label' 
        'content:' content=STRING
;

Form:
    'Form' (name=ID)?
    elements*=FormElement
    'endform'
;

FormElement:
    Input | Label | Button
;


Image:
    'Image'
    ( 
        ('width:' width=INT)?
        ('height:' height=INT)?
        ('source:' (source=[Attribute|ID] | source_static=STRING))?
    )#
;

Text:
    'Text' 
    'content:' (
        content_path=NestedAccessPath          
        | content=[Attribute|ID]                  
        | content_static=STRING               
    )
    (
        ('size:' size=INT)?
        ('color:' color=STRING)?
    )#
;

Notification:
    'Notification'
    ('type:' type=NotificationType)?
    'message:' message=[Attribute|ID]
;

NotificationType:
    'info' | 'warning' | 'error' | 'success'
;

LineChart:
    'LineChart' 
    (
        ('xLabel:' xLabel=STRING)
        ('yLabel:' yLabel=STRING)
        ('xValue:' (xValue=[Attribute|ID] | xValue_static=STRING))
        ('yValues:' (yValues+=[Attribute|ID][','] | yValues_static+=STRING[',']))
        ('staticData:' staticData=List)?
    )#
;

LiveTable:
    'LiveTable'
    'columns:' columns+=[Attribute|ID][',']
;

Gauge:
    'Gauge' 
    'value:' (value=[Attribute|ID] |  value_static=FLOAT)
;

JsonViewer:
    'JsonViewer'
    ('attributes:' attributes+=[Attribute|ID][','])?
;

Alive:
    'Alive'
    'timeout:' timeout=INT
;

Publish:
    'Publish'
    ('broker:' broker=[MessageBroker])?
    ('endpoint:' endpoint=[RESTEndpoint])?
    (
        ('topic:' topic=STRING)?
        ('json:' json=STRING)?
    )#
;

Logs:
    'Logs'
    ('attributes:' attributes+=[Attribute|ID][','])
;

CrudTable:
    'CrudTable'
    ('attributes:' attributes+=[Attribute|ID][','])?
;

