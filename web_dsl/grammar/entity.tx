import sources
import types
import connections

EntitySource: (MySQLQuery | MongoDBQuery | RESTEndpoint | BrokerTopic);

Entity:
    'Entity' name=ID
    (
        ('description:' description=STRING)?
        'source:' source=[EntitySource]
        'attributes:' '-' attributes*=Attribute['-']
        ('strict:' strict=BOOL)?
    )#
    'end'
;

Attribute:
    IntAttribute        |
    FloatAttribute      |
    BoolAttribute       |
    StringAttribute     |
    ListAttribute       |
    DictAttribute
;

IntAttribute:
    name=ID ':' 'int' ('=' default=INT)?
;

FloatAttribute:
    name=ID ':' 'float' ('=' default=FLOAT)?
;

StringAttribute:
    name=ID ':' 'str' ('=' default=STRING)?
;

BoolAttribute:
    name=ID ':' 'bool' ('=' default=BOOL)?
;

ListAttribute:
    name=ID ':' 'list' ('=' '[' items*=Attribute[','] ']')?
;


DictAttribute:
    name=ID ':' 'dict' ('=' '{' items*=Attribute[','] '}')?
;
