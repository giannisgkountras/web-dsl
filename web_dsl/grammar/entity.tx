import communication
import types

EntitySource: (MessageBroker | RESTEndpoint | Database);

Entity:
    'Entity' name=ID
    (
        ('description:' description=STRING)?
        'source:' source=[EntitySource]
        'attributes:' '-' attributes*=Attribute['-']

        ('topic:' topic=STRING)?

        ('path:' path=STRING)?
        ('method:' method=RESTVerb)?
        ('body:' body=Dict)?
        ('params:' params=Dict)?

        ('database:' database=STRING)?
        ('query:' query=STRING)?
        ('collection:' collection=STRING)?
        ('filter:' filter=Dict)?

    )#
    'end'
;

Attribute:
    IntAttribute        |
    FloatAttribute      |
    BoolAttribute       |
    StringAttribute     |
    ListAttribute       |
    DictAttribute
;

IntAttribute:
    name=ID ':' 'int' ('=' default=INT)?
;

FloatAttribute:
    name=ID ':' 'float' ('=' default=FLOAT)?
;

StringAttribute:
    name=ID ':' 'str' ('=' default=STRING)?
;

BoolAttribute:
    name=ID ':' 'bool' ('=' default=BOOL)?
;

ListAttribute:
    name=ID ':' 'list' ('=' '[' items*=Attribute[','] ']')?
;


DictAttribute:
    name=ID ':' 'dict' ('=' '{' items*=Attribute[','] '}')?
;
