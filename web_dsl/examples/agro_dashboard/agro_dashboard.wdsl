Webpage AgroDashboard
    author: "Giannis Gkountras"
    version: "1.0"
    description: "A dashboard for agriculture"
    navbar: Home

    //============ Connections =============//

    API backendAPI
        host: "0.0.0.0"
        port: 8321
    end

    Websocket backendWebsocket
        host: "192.168.1.6"
        port: 8080
    end

    Database<MySQL> AgroSQL
        host: "192.168.1.6"
        port: 3306
        auth:
            username: "thesis"
            password: "password"
        database: "thesis"
    end

    Database<MongoDB> AgroMongoDB
        host: "192.168.1.6"
        database: "thesis"
        port: 27017
        auth:
            username: "thesis"
            password: "password"
    end

    RESTApi CropAPI
        host: "http://192.168.1.6:4000"
        headers: {"Content-Type": "application/json"}
    end

    Broker<MQTT> FieldBroker
        host: "192.168.1.6"
        port: 1883
    end

    //============ Specific endpoints/queries =============//
    
    MySQLQuery soilMoistureZone1
        connection: AgroSQL
        query: "SELECT timestamp, moisture_percent FROM soil_moisture_readings WHERE zone_id=1"
    end

    MySQLQuery averageMoisturePerZone
        connection: AgroSQL
        query: "SELECT z.name AS zone, 
                    ROUND(AVG(s.moisture_percent), 2) AS avg_moisture
                FROM soil_moisture_readings s
                JOIN zones z ON s.zone_id = z.id
                GROUP BY z.id;"
    end

    MySQLQuery CropDistribution
        connection: AgroSQL
        query: "SELECT crop_type, area_percent
                FROM crop_distribution
                WHERE zone_id = 1;"
    end

    MongoDBQuery CropIrrigation
        connection: AgroMongoDB
        collection: "irrigation_zones" 
    end

    MongoDBQuery CropInfo
        connection: AgroMongoDB
        collection: "crops"
    end

    RESTEndpoint CreateCrop
        connection: CropAPI
        path: "/crop"
        method: POST
    end

    BrokerTopic FieldCamera
        connection: FieldBroker
        topic: "camera"
    end

     //============ Entities ============//

    Entity moistureZone1
        source: soilMoistureZone1
        attributes:
            - timestamp: list
            - moisture_percent: list
    end

    Entity averageMoisture
        source: averageMoisturePerZone
        attributes:
            - zone: list
            - avg_moisture: list
    end

    Entity cropDistributionData
        source: CropDistribution
        attributes:
            - crop_type: list
            - area_percent: list
    end

    Entity CropInfoData
        source: CropInfo
        attributes:
            - name: list
            - type: list
            - harvet_season: list
            - yield_kg_per_hectare: list
            - irrigation_required: bool
    end

    Entity CropIrrigationData
        source: CropIrrigation
        attributes:
            - data: list
    end

    Entity CropCreationData
        source: CreateCrop
        attributes:
            - name: str
            - type: str
            - price_per_kg: float
            - available: bool
    end

    Entity FieldCameraData
        source: FieldCamera
        attributes:
            - frame: str
            - motion: bool
        strict: true
    end

     //============ Components ============//

    Component MoistureChartZone1
        entity: moistureZone1
        type: LineChart
            description: "Soil Moisture Zone 1"
            xLabel: "Time"
            yLabel: "Soil Moisture"
            xValue: this.timestamp
            yValues: this.moisture_percent
    end

    Component AverageMoistureChart
        entity: averageMoisture
        type: BarChart
            description: "Average Moisture Per Zone"
            xLabel: "Zone"
            yLabel: "Avg Moisture"
            xValue: this.zone
            yValues: this.avg_moisture
    end

    Component CropDistributionPieChart
        entity: cropDistributionData
        type: PieChart
            description: "Crop Distribution Area"
            value: this.area_percent
            dataName: this.crop_type
    end

    Component CropsTable
        entity: CropInfoData
        type: Table
            primary_key: "_id"
            attributes: this.name, this.type, this.yield_kg_per_hectare
    end

    Component CropIrrigationJSONViewer
        entity: CropIrrigationData
        type: JsonViewer
    end

    Component CropForm
        entity: CropCreationData
        type: Form
                description: "Add new crops"
                Label "Name"
                Input
                    type: text
                    placeholder: "Crop name"
                    required: true
                    datakey: "name"
                Label "Type"
                Input 
                    type: text 
                    placeholder: "Crop type" 
                    required: true
                    datakey: "type"
                Label "Price"
                Input 
                    type: number 
                    placeholder: "Price per kg" 
                    required: true            
                    datakey: "price"
    end

    Component FieldLiveCamera
        entity: FieldCameraData
        type: Image
            source: this.frame
            width: 400
            height: 300
    end

    Component FieldLiveCameraAlive
        entity: FieldCameraData
        type: Alive
                timeout: 5000
    end

     //============ Screens ============//

    Screen Home
        title: "Home"
        url: "/"

        row
            col
                use MoistureChartZone1
            endcol
            col
                use AverageMoistureChart
            endcol
            col
                use CropDistributionPieChart
            endcol
        endrow

        row
            col
                use CropsTable
            endcol
            col
                use CropIrrigationJSONViewer
            endcol
            col
                use CropForm
            endcol
            col
                if FieldCameraData.motion == true
                    use FieldLiveCamera
                    
                if FieldCameraData.motion == false
                    use FieldLiveCameraAlive
            endcol
        endrow

    end


     //============ Debug ============//

    Screen Debug
        title: "Debug"
        url: "/debug"

        row
            col
                Component FieldCameraPublisher
                    type: Publish
                    broker: FieldBroker
                end
            endcol
            col
                link to: "/" text:"Hello world"
            endcol
        endrow
        row
            use CropsTable
        endrow
    end