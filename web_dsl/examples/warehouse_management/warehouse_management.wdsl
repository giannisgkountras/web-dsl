webpage WarehouseManagement
    author: "Giannis Gkountras"
    version: "1.0"
    description: "A dashboard for managing warehouses"
    navbar: true

    //============ Connections =============//

    API backendAPI
        host: "0.0.0.0"
        port: 8321
    end

    Websocket backendWebsocket
        host: "192.168.1.6"
        port: 8080
    end

    Database<MySQL> WarehouseSQL
        host: "192.168.1.6"
        port: 3306
        auth:
            username: "thesis"
            password: "password"
        database: "thesis"
    end

    Database<MongoDB> WarehouseMongo
        host: "192.168.1.6"
        database: "thesis"
        port: 27017
        auth:
            username: "thesis"
            password: "password"
    end

    Broker<MQTT> NotificationsBroker
        host: "192.168.1.6"
        port: 1883
    end

    //============ Specific endpoints/queries/ topics =============//

    MySQLQuery allProducts
        connection: WarehouseSQL
        query: "SELECT * from products"
    end

    MySQLQuery salesCategory
        connection: WarehouseSQL
        query: "SELECT * from sales_by_category"
    end

    MySQLQuery yearlyIncome
        connection: WarehouseSQL
        query: "SELECT
                    year,
                    SUM(CASE WHEN category = 'Electronics' THEN income ELSE 0 END) AS Electronics,
                    SUM(CASE WHEN category = 'Furniture' THEN income ELSE 0 END) AS Furniture,
                    SUM(CASE WHEN category = 'Clothing' THEN income ELSE 0 END) AS Clothing,
                    SUM(CASE WHEN category = 'Toys' THEN income ELSE 0 END) AS Toys
                FROM
                    yearly_income
                GROUP BY
                    year
                ORDER BY
                    year;"
    end

    MongoDBQuery allWarehouses
        connection: WarehouseMongo
        collection: "warehouses"
    end

    MongoDBQuery allRevenue
        connection: WarehouseMongo
        collection: "revenue"
    end

    BrokerTopic Notifications
        connection: NotificationsBroker
        topic: "notifications"
    end

    BrokerTopic Chart
        connection: NotificationsBroker
        topic: "chart"
    end


     //============ Entities ============//
    Entity Products
        source: allProducts
        strict: true
        attributes:
            - id: list
            - name: list
            - category: list
            - description: list
            - price: list
    end

    Entity Warehouses
        source: allWarehouses
        strict: true
        attributes:
            - name: str
            - capacity: int
            - address: str
            - isOperational: bool
    end

    Entity Revenue
        source: allRevenue
        attributes:
            - date: str
            - amount: float
    end

    Entity SalesByCategory
        source: salesCategory
        attributes:
            - category: str
            - total_sales: float
    end

    Entity YearlyIncomeData
        source: yearlyIncome
        attributes:
            - year: int
            - income: float
    end

    Entity NotificationsData
        source: Notifications
        attributes:
            - info: str
            - warning: str
            - error: str
    end

    Entity ChartDebugData
        source: Chart
        attributes:
            - x: int
            - y1: int
            - y2: int
    end
    //============ Components ============//
    Component ProductsTable
        entity: Products
        type: Table
            description: "All products"
            primary_key: "id"
            table: "products"
            crud: true
    end

    Component WarehousesTable
        entity: Warehouses 
        type: Table
            description: "All warehouses"
            primary_key: "_id"
            attributes: this.name, this.capacity, this.address, this.isOperational
            crud: false
    end

    Component RevenueBarChart
        entity: Revenue
        type: BarChart
            description: "Revenue over time"
            xLabel: "Time"
            yLabel: "Money"
            xValue: this.date
            yValues: this.amount
    end

    Component CategorySalesPieChart
        entity: SalesByCategory
        type: PieChart
            description: "Sales by Category"
            value: this.total_sales
            dataName: this.category
    end

    Component YearlyIncomeChart
        entity: YearlyIncomeData
        type: LineChart
            description: "Yearly income"
            xLabel: "Time"
            yLabel: "Money"
            xValue: this.year
            yValues: this.Electronics, this.Furniture, this.Clothing, this.Toys
    end

    Component InfoNotifications
        entity: NotificationsData
        type: Notification
            type: info
            message: this.info
    end

    Component WarningNotifications
        entity: NotificationsData
        type: Notification
            type: warning
            message: this.warning
    end

    Component WarehouseOperational
        type: Text
            content: "Warehouse is operational"
            color: "#8EC07C"
            size: 20
    end

    Component WarehouseNotOperational
        type: Text
            content: "Warehouse is not operational"
            color: "#FA4934"
            size: 22
    end

    Component WarehouseNotificationsAlive
        entity: NotificationsData
        type: Alive
            timeout: 5000
            description: "Notifications channel"
    end

    Component ChartDebug
        entity: ChartDebugData
        type: LineChart
            description: "Debug chart"
            xLabel: "X Axis"
            yLabel: "Y axis"
            xValue: this.x
            yValues: this.y1, this.y2
    end

    Component CentralWarehouseCapacity
        entity: Warehouses
        type: ProgressBar
            value: this.capacity[0]
            max: this.capacity[2]
            description: "Central warehouse capacity"
    end
    //============ Screens ============//

    Screen Home
        title: "Home"
        url: "/"

        use InfoNotifications
        use WarningNotifications

        row
            col
                use ProductsTable
            endcol
            col
                use WarehousesTable
            endcol
        endrow
        row
            col
                use RevenueBarChart
            endcol
            col
                use CategorySalesPieChart
            endcol
            col
                use YearlyIncomeChart
            endcol
            col
                if this.isOperational[0] == true from entity Warehouses
                        use WarehouseNotificationsAlive, WarehouseOperational, CentralWarehouseCapacity
                        else use WarehouseNotOperational
                        every 2000
            endcol
        endrow
    end

    Screen Debug
        title: "Debug"
        url: "/debug"

        row
            col
                Component NotificationsPublisher
                    type: Publish
                        broker: NotificationsBroker
                end
            endcol
        endrow
        row
            use ChartDebug
        endrow
    end