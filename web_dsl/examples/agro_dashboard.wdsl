webpage AgroDashboard
    author: "Giannis Gkountras"
    version: "1.0"
    description: "A dashboard for agriculture"
    navbar: true

    //============ Connections =============//

    API backendAPI
        host: "0.0.0.0"
        port: 8321
    end

    Websocket backendWebsocket
        host: "192.168.1.6"
        port: 8080
    end

    Database<MySQL> AgroSQL
        host: "192.168.1.6"
        port: 3306
        auth:
            username: "thesis"
            password: "password"
        database: "thesis"
    end

    Database<MongoDB> AgroMongoDB
        host: "192.168.1.6"
        database: "thesis"
        port: 27017
        auth:
            username: "thesis"
            password: "password"
    end

    RESTApi CropAPI
        host: "http://192.168.1.6:3000"
        headers: {"Content-Type": "application/json"}
    end

    //============ Specific endpoints/queries =============//
    
    MySQLQuery soilMoistureZone1
        connection: AgroSQL
        query: "SELECT timestamp, moisture_percent FROM soil_moisture_readings WHERE zone_id=1"
    end

    MySQLQuery averageMoisturePerZone
        connection: AgroSQL
        query: "SELECT z.name AS zone, 
                    ROUND(AVG(s.moisture_percent), 2) AS avg_moisture
                FROM soil_moisture_readings s
                JOIN zones z ON s.zone_id = z.id
                GROUP BY z.id;"
    end

    MySQLQuery CropDistribution
        connection: AgroSQL
        query: "SELECT crop_type, area_percent
                FROM crop_distribution
                WHERE zone_id = 1;"
    end

    MongoDBQuery CropIrrigation
        connection: AgroMongoDB
        collection: "irrigation_zones" 
    end

    MongoDBQuery CropInfo
        connection: AgroMongoDB
        collection: "crops"
    end

    RESTEndpoint CreateCrop
        connection: CropAPI
        path: "/crop"
        method: POST
    end

     //============ Entities ============//

    Entity moistureZone1
        source: soilMoistureZone1
        attributes:
            - timestamp: list
            - moisture_percent: list
    end

    Entity averageMoisture
        source: averageMoisturePerZone
        attributes:
            - zone: list
            - avg_moisture: list
    end

    Entity cropDistributionData
        source: CropDistribution
        attributes:
            - crop_type: list
            - area_percent: list
    end

    Entity CropInfoData
        source: CropInfo
        attributes:
            - name: list
            - type: list
            - harvet_season: list
            - yield_kg_per_hectare: list
            - irrigation_required: bool
    end

    Entity CropIrrigationData
        source: CropIrrigation
        attributes:
            - data: list
    end

    Entity CropCreationData
        source: CreateCrop
        attributes:
            - name: str
            - type: str
            - price_per_kg: float
            - available: bool
    end

     //============ Components ============//

    Component MoistureChartZone1
        entity: moistureZone1
        type: LineChart
            description: "Soil Moisture Zone 1"
            xLabel: "Time"
            yLabel: "Soil Moisture"
            xValue: this.timestamp
            yValues: this.moisture_percent
    end

    Component AverageMoistureChart
        entity: averageMoisture
        type: BarChart
            description: "Average Moisture Per Zone"
            xLabel: "Zone"
            yLabel: "Avg Moisture"
            xValue: this.zone
            yValues: this.avg_moisture
    end

    Component CropDistributionPieChart
        entity: cropDistributionData
        type: PieChart
            description: "Crop Distribution Area"
            value: this.area_percent
            dataName: this.crop_type
    end

    Component CropsTable
        entity: CropInfoData
        type: CrudTable
            attributes: this.name, this.type, this.harvest_season, this.yield_kg_per_hectare
    end

    Component CropIrrigationJSONViewer
        entity: CropIrrigationData
        type: JsonViewer
    end

    Component CropForm
        entity: CropCreationData
        type: Form
                Label "Name"
                Input
                    type: text
                    placeholder: "Crop name"
                    required: true
                Label "Type"
                Input 
                    type: text 
                    placeholder: "Crop type" 
                    required: true
                Label "Price"
                Input 
                    type: number 
                    placeholder: "Price per kg" 
                    required: true
                Label "Availability"
                Input 
                    type: checkbox 
                    placeholder: "Availability" 
                    required: true
    end


     //============ Screens ============//

    Screen Home
        title: "Home"
        url: "/"

        row
            col
                use MoistureChartZone1
            endcol
            col
                use AverageMoistureChart
            endcol
            col
                use CropDistributionPieChart
            endcol
        endrow

        row
            col
                use CropsTable
            endcol
            col
                use CropIrrigationJSONViewer
            endcol
            col
                use CropForm
            endcol
        endrow
    end