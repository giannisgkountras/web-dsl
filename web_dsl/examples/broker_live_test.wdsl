webpage Vroomitor
    author: "Giannis Gkountras"
    version: "1.0"
    description: "A live dashboard"
    navbar: true

    API publisherAPI
        host: "0.0.0.0"
        port: 8321
    end

    Broker<MQTT> HomeBroker
        host: "192.168.1.6"
        port: 1883
    end

    Broker<Redis> LabRedis
        host: "192.168.1.6"
        port: 6379
    end

    Websocket HomeWebsocket
        host: "192.168.1.6"
        port: 8080
    end

    Entity NotificationData
        source: HomeBroker
        attributes:
            - message: str
    end

    Entity AllLogsData
        source: LabRedis
        attributes:
            - speed: float
            - car_running: str
            - current_state: str
    end

    DefineComponent infoNotification as
        LiveComponent
            topic: "notifications/info"
            dataName: "notificationsInfo"
            content: Notification infoNotification type: info
            entity: NotificationData
        end
    end


    Screen Dashboard
        title: "Dashboard"
        url: "/"

        Entity SensorData
            source: HomeBroker
            attributes: 
                - value: float
        end

        Entity SpeedChartData
            source: HomeBroker
            attributes:
                - timestamp: str
                - speed: float
                - cpu: float
                - gpu: float
        end

        Entity GaugeData
            source: HomeBroker
            attributes:
                - cpu: int
        end

        Entity LiveImageData
            source: HomeBroker
            attributes:
                - frame: str
        end

        Entity JsonViewerData
            source: HomeBroker
            attributes:
                - data: dict
        end

        row
            col 
                row
                     p "Humidity Sensor:"
                    LiveComponent
                        topic: "sensor"
                        dataName: "latestReading"
                        content: p "Latest sensor value"
                        entity: SensorData
                    end
                endrow
                row
                    LiveComponent
                        topic: "view"
                        dataName: "jsonData"
                        content:
                            JsonViewer dataViewer
                        entity: JsonViewerData
                    end
                endrow
            endcol
            col
                LiveComponent
                    topic: "chart"
                    dataName: "speedChart"
                    content: LineChart speedChart
                                xLabel: "Time"
                                yLabel: "Speed"
                    entity: SpeedChartData
                end
            endcol
            col
                LiveComponent
                    topic: "logs"
                    dataName: "allLogs"
                    content: Logs allLogs
                    entity: AllLogsData
                end
            endcol
        endrow
        row
            col
                row
                    LiveComponent
                        topic: "gauge"
                        dataName: "gaugeValue"
                        content: Gauge cpuGauge
                        entity: GaugeData
                    end
                endrow
                row
                    LiveComponent
                        topic: "gauge"
                        dataName: "aliveValue"
                        content: Alive gaugeAlive
                                    timeout: 5000
                        entity: GaugeData
                    end
                endrow
            endcol
            col
                LiveComponent
                    topic: "image"
                    dataName: "liveImg"
                    content: 
                        Image liveimg
                        width: 400
                        height: 400 
                    entity: LiveImageData
                end
            endcol
            col
                row
                    Publish HomeBrokerPublisher
                        broker: HomeBroker
                endrow
            endcol
        endrow
        useComponent infoNotification
    end